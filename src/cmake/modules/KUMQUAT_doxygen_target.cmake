# add a target to generate API documentation with Doxygen
find_package(Doxygen QUIET)
if(DOXYGEN_FOUND)
  set(KUMQUAT_MODULES ${KUMQUAT_MODULES} "cpp-documentation" CACHE INTERNAL "KUMQUAT_MODULES")

  # starting from cmake 3.9 the usage of DOXYGEN_EXECUTABLE is deprecated
  if(TARGET Doxygen::doxygen)
    get_property(DOXYGEN_EXECUTABLE TARGET Doxygen::doxygen PROPERTY IMPORTED_LOCATION)
  endif()

  message("++ Project = ${CMAKE_PROJECT_NAME}")
  if (CMAKE_PROJECT_NAME STREQUAL "KUMQUATdev")
    # Set Doxyfile.in variables for the developer version
    set(KUMQUAT_DOXYGEN_SOURCE_PREFIX "${CMAKE_SOURCE_DIR}/src")
    foreach(KUMQUAT_MODULE ${KUMQUAT_MODULES_FULL_LIST})
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/doc/")
        set(KUMQUAT_DOXYGEN_IMAGE_PATH "${KUMQUAT_DOXYGEN_IMAGE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/doc/ \\  \n")
      endif()
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/example/")
        set(KUMQUAT_DOXYGEN_EXAMPLE_PATH "${KUMQUAT_DOXYGEN_EXAMPLE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/example/ \\  \n")
      endif()
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/utilities/")
        set(KUMQUAT_DOXYGEN_EXAMPLE_PATH "${KUMQUAT_DOXYGEN_EXAMPLE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/${KUMQUAT_MODULE}/utilities/ \\  \n")
      endif()
    endforeach(KUMQUAT_MODULE ${KUMQUAT_MODULES_FULL_LIST})
    set(KUMQUAT_DOXYGEN_COMMON_DOC_PATH "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/common/doc")
    set(KUMQUAT_DOXYGEN_UTILS_PATH "*/utilities")
    endif()
  if (CMAKE_PROJECT_NAME STREQUAL "KUMQUAT")
    # Set Doxyfile.in variables for the user version
    set(KUMQUAT_DOXYGEN_SOURCE_PREFIX "${CMAKE_SOURCE_DIR}")
    foreach(KUMQUAT_MODULE ${KUMQUAT_MODULES_FULL_LIST})
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/doc/${KUMQUAT_MODULE}")
        set(KUMQUAT_DOXYGEN_IMAGE_PATH "${KUMQUAT_DOXYGEN_IMAGE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/doc/${KUMQUAT_MODULE}/ \\  \n")
      endif()
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/example/${KUMQUAT_MODULE}")
        set(KUMQUAT_DOXYGEN_EXAMPLE_PATH "${KUMQUAT_DOXYGEN_EXAMPLE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/example/${KUMQUAT_MODULE}/ \\  \n")
      endif()
      if(EXISTS "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/utilities/${KUMQUAT_MODULE}")
        set(KUMQUAT_DOXYGEN_EXAMPLE_PATH "${KUMQUAT_DOXYGEN_EXAMPLE_PATH}    ${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/utilities/${KUMQUAT_MODULE}/ \\  \n")
      endif()
    endforeach(KUMQUAT_MODULE ${KUMQUAT_MODULES_FULL_LIST})
    set(KUMQUAT_DOXYGEN_COMMON_DOC_PATH "${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/doc/common")
    set(KUMQUAT_DOXYGEN_UTILS_PATH "utilities/*")
  endif()

  message("++ Doxygen version ${DOXYGEN_VERSION}")
  if (DOXYGEN_VERSION VERSION_LESS 1.9.3)
    set(KUMQUAT_DOXYGEN_CLASS_DIAGRAMS "CLASS_DIAGRAMS = NO")
  else()
    set(KUMQUAT_DOXYGEN_CLASS_DIAGRAMS "")
  endif()
  if (DOXYGEN_VERSION VERSION_LESS 1.9.2)
    set(KUMQUAT_DOXYGEN_MATHJAX_VERSION "MATHJAX_VERSION = MathJax_2")
    set(KUMQUAT_DOXYGEN_MATHJAX_EXTENSIONS "TeX/AMSmath TeX/AMSsymbols")
  else()
    set(KUMQUAT_DOXYGEN_MATHJAX_VERSION "MATHJAX_VERSION = MathJax_3")
    set(KUMQUAT_DOXYGEN_MATHJAX_EXTENSIONS "ams")
  endif()

  configure_file(${KUMQUAT_DOXYGEN_SOURCE_PREFIX}/Doxyfile.in "${CMAKE_CURRENT_BINARY_DIR}/Doxyfile" @ONLY)

  add_custom_target(doxygen ${DOXYGEN_EXECUTABLE} ${CMAKE_CURRENT_BINARY_DIR}/Doxyfile
                    WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
                    COMMENT "Generating API documentation with Doxygen in 'html' directory" VERBATIM)
else()
  set(KUMQUAT_MISSING_MODULES ${KUMQUAT_MISSING_MODULES} "cpp-documentation" CACHE INTERNAL "KUMQUAT_MISSING_MODULES")
endif()
